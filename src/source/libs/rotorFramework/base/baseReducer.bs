
' namespace Reducers

class Reducer

    model as object
    middlewares = []
    dispatch as Function

    getDispatcher as function
    ownerDispatcher as object
    ownerDispatcherId as string

    sub new()

        m.getDispatcher = function(dispatcherId as string)
            return GetGlobalAA().rotor_framework_helper.frameworkInstance.dispatcherProvider.getFacade(dispatcherId, m)
        end function

        m.dispatch = function(intent as Interface.intent)
            return m.ownerDispatcher.dispatch(intent)
        end function

    end sub

    public function reducer(state as object, intent as Intent)
        return state
    end function

    public function applyMiddlewares() as object
        return []
    end function

    function reduce(state as object, intent) as object
        if intent.Count() > 1 and intent.payload = invalid then throw "[WARNING] Intent payload is invalid."

        ' Resolve middlewares
        middlewares = m.applyMiddlewares()
        mwIndex = 0
        mwCount = middlewares.Count()
        while intent <> invalid and mwIndex < mwCount
            middlewareFnScoped = middlewares[mwIndex]
            middlewareFnScoped = middlewareFnScoped
            intent = middlewareFnScoped(intent, state)
            mwIndex++
        end while
        middlewareFnScoped = invalid ' invalidate scoped fn
        if intent = invalid then return invalid

        ' Resolve Reducer
        newState = m.reducer(state, intent)

        return newState

    end function

    sub destroy()
        m.ownerDispatcher = invalid
    end sub

end class

' end namespace
