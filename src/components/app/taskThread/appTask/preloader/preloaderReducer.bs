namespace Reducers

    class PreloaderReducer extends Reducer

        homeReaderDisparcher as Dispatcher

        sub logIntent(intent)
            print `[PRELOADER REDUCER][INFO] dispatcherId: ${m.ownerDispatcherId}, type: ${intent.type}`; intent?.payload?.preloadId <> invalid ? `, preloadId: ${intent.payload?.preloadId}` : ""
        end sub

        override function applyMiddlewares() as object
            return [

                ' (1) Logger middleware
                function(intent, state) as Intent
                    m.logIntent(intent)
                    return intent ' NEXT
                end function,

                ' (2) Aynchronous resources
                function(intent, state) as Intent
                    if intent.type = IntentType.PRELOADER.START_PRELOAD_RESOURCES

                        m.preloaderDispatcher = m.getDispatcher("preloader")
                        m.appStoreDispatcher = m.getDispatcher("appStore")

                        m.preloadHomeFeed()
                        m.fetchResources()

                        return invalid
                    end if

                    return intent
                end function,

            ]
        end function

        override function reducer(state as Object, intent as Intent)

            if intent.type = IntentType.PRELOADER.ADD_ITEM
                state.counter++
                state.maxHit++

            else if intent.type = IntentType.PRELOADER.COMPLETED_ITEM
                state.counter--
            end if

            if state.counter = 0
                state.resourcesReady = true
            else
                return invalid ' possibly to stop refresh state
            end if

            return state
        end function

        override sub destroy()
            m.homeReaderDisparcher.destroy("homeFeedReader")
        end sub


        ' Cutsom reducer methods

        sub preloadHomeFeed()
            ' (1) dispatch add item
            m.dispatch({
                type: IntentType.PRELOADER.ADD_ITEM,
                payload: {
                    preloadId: "preloadHomeFeed"
                }
            })

            m.homeReaderDisparcher = m.getDispatcher("homeFeedDispatcher")

            ' (2) dispatch completed item
            m.homeReaderDisparcher.addListener({
                callbackWithState: sub(state)
                    if state.loading = false
                        m.dispatch({
                            type: IntentType.PRELOADER.COMPLETED_ITEM,
                            payload: {
                                preloadId: "preloadHomeFeed"
                            }
                        })
                    end if
                end sub,
                once: true
            })
            ' (3) start loading home feed
            m.homeReaderDisparcher.dispatch({
                type: IntentType.CONTENT_READER.START_LOADING
            })

        end sub



        sub fetchResources() ' simulation

            ' (1) Dispatch add item
            m.preloaderDispatcher.dispatch({
                type: IntentType.PRELOADER.ADD_ITEM,
                payload: {
                    preloadId: "loadingScreenMinTime"
                }
            })

            ' (2) Get minimum loading duration
            appStoreState = m.appStoreDispatcher.getState()
            loadingScreenMinTime = appStoreState.loadingScreenMinTime

            ' (3) dispatch completed item, if done
            sleepDispatcher = m.getDispatcher("sleep")
            sleepDispatcher.addListener({
                callback: sub()
                    m.dispatch({
                        type: IntentType.PRELOADER.COMPLETED_ITEM,
                        payload: {
                            preloadId: "loadingScreenMinTime"
                        }
                    })
                end sub,
                once: true
            })

            ' (4) Start async process
            sleepDispatcher.dispatch({
                type: IntentType.SLEEP.START,
                payload: {
                    duration: loadingScreenMinTime
                }
            })


        end sub

    end class

end namespace
