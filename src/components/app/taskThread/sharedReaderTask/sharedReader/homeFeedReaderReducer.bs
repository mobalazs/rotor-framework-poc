import "../../../common/httpLogic/httpRouter.bs"
import "sharedLogic/sharedReaderReducer.bs"

namespace Reducers

    class HomeFeedReaderReducer extends SharedReaderReducer

        ' logic on top of the basic reducer class
        reducerParams = {
            url: "https://rotor-sample.b-cdn.net/mockups/feed/home_feed.json",
            useMockup: true,
            expiration: -1
        }

        override function parser(data) as object
            createContentNode = Rotor.Utils.createContentNode

            feedContent = createContentNode({
                status: data.Count() > 0 ? "operational" : "down"
            })

            contentTable = {}
            for each contentItemData in data.contents
                contentTable[contentItemData.contentId] = contentItemData
            end for

            data.rows.SortBy("sort")
            for each rowItemData in data.rows
                rowNode = createContentNode({
                    rowType: rowItemData. rowType,
                    title: rowItemData.title
                })

                for each contentId in rowItemData.contentList
                    tileData = contentTable[contentId]
                    tileContentNode = createContentNode(tileData)
                    rowNode.appendChild(tileContentNode)
                end for

                feedContent.appendChild(rowNode)
            end for

            return feedContent
        end function


    end class

end namespace
