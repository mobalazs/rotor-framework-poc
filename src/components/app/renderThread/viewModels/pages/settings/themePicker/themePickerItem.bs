namespace ViewModels

    class ThemePickerItem extends ViewModel

        override sub onUpdateView()
            itemIconWidget = m.getWidget("itemIcon")
            itemIconWidget.refresh("fields.blendColor")
            itemLabel = m.getWidget("itemLabel")
            itemLabel.refresh("fields.color")
        end sub

        override function template() as object

            focus = {
                onFocusChanged: sub (isFocused) ' onSelected pass isFocused
                    m.onUpdateView()
                    if isFocused = true and m.props.isAutoSelectOnFocusEnabled = true
                        m.context.notifySelection(m.props.optionKey, m.props.isActive)
                    end if
                end sub,
                onSelected: sub () ' onSelected pass isFocused
                    m.context.notifySelection(m.props.optionKey, m.props.isActive)
                end sub
            }

            return {
                ' id: `option-${assetKey}`,
                theme: ["menu.optionlistContent.themePickerItem", "settings.themePicker", "menu.shared.menuItemColor"],
                sgName: "Group",
                focus: focus,
                context: {
                    isFocused: false, ' as default
                    notifySelection: sub(themeKey, isActive)
                        if isActive = false
                            m.dispatcher.user.dispatch({
                                type: IntentType.USER.SET_ACTIVE_THEME_KEY,
                                payload: {
                                    activeThemeKey: themeKey
                                }
                            })
                        end if
                    end sub
                },
                dispatcher: "user",
                fields: {
                    rotation: "@context.theme.settings.themePicker.rotation",
                    scaleRotateCenter: function() as object
                        themePickerTheme = m.context.theme.settings.themePicker
                        themePickerThemeTheme = m.context.theme.menu.optionlistContent.themePickerItem
                        return [themePickerTheme.labelWidth + themePickerThemeTheme.marginRight * 1.5, themePickerThemeTheme.height / 2]
                    end function,
                    translation: function() as object ' expression example
                        theme = m.context.theme.menu.optionlistContent.themePickerItem
                        return [0, m.props.optionIndex * (theme.height + (theme.marginBottom ?? 0))]
                    end function
                },
                children: [
                    {
                        id: "itemIcon",
                        sgName: "Poster",
                        fields: {
                            width: "#theme.menu.shared.icon.size.width",
                            height: "#theme.menu.shared.icon.size.height",
                            uri: "#theme.settings.themePicker.icon.url",
                            blendColor: function() as string
                                optionKey = m.props.optionKey
                                isActive = m.props.isActive
                                theme = m.context.theme
                                if m.context.isFocused
                                    return theme.settings.themePicker.colors_focused[optionKey]
                                else
                                    return isActive ? theme.settings.themePicker.colors_focused[optionKey] : theme.settings.themePicker.colors[optionKey]
                                end if
                            end function,
                            translation: function() ' expression example
                                return [m.context.theme.settings.themePicker.labelWidth + m.context.theme.menu.optionlistContent.themePickerItem.marginRight, 0]
                            end function
                        }
                    }, { ' old
                        id: "itemLabel",
                        sgName: "Label",
                        tr: `themeSamples.${m.props.optionKey}`,
                        fontStyle: "#theme.fontStyles.subMenuLabelFontStyle",
                        fields: {
                            color: function() as string
                                if m.context.isFocused
                                    return m.context.theme.menu.shared.menuItemColor.focused
                                else
                                    return m.props.isActive ? m.context.theme.menu.shared.menuItemColor.active : m.context.theme.menu.shared.menuItemColor.default
                                end if
                            end function,
                            text: function()
                                return m.context.tr.themeSamples[m.props.optionKey]
                            end function,
                            width: "#theme.settings.themePicker.labelWidth",
                            height: "#theme.menu.shared.icon.size.height",
                            vertAlign: "center",
                            horizAlign: "right"
                        }
                    }
                ]
            }

        end function

    end class

end namespace
